plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.asciidoctor.convert" version "1.5.9.2"
    id 'java'

    //https://github.com/n0mer/gradle-git-properties - git 관련 info plugin
    id "com.gorylenko.gradle-git-properties" version "2.3.1"
}

group = 'com.cenibee.book.springreactive'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// gradle 에서 프로젝트 관련 정보 info 에 뽑아내기
springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-rsocket'
    implementation ('org.springframework.boot:spring-boot-starter-hateoas') {
        exclude module: 'spring-boot-starter-web'
    }
//    implementation 'org.springframework.hateoas:spring-hateoas:1.3.2'
    implementation 'org.mongodb:mongodb-driver-sync'
    implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'

    implementation 'io.projectreactor.tools:blockhound:1.0.6.RELEASE'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'org.springframework.boot:spring-boot-devtools'

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
//    testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.6.RELEASE'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.7.2'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'

    testImplementation 'org.testcontainers:testcontainers-bom:1.15.3'
    testImplementation 'org.testcontainers:rabbitmq:1.15.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.3'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}


bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}